# 第三次实验报告

## ECN 实验

### ecn.p4

```c
/* -*- P4_16 -*- */
#include <core.p4>
#include <v1model.p4>

const bit<8>  TCP_PROTOCOL = 0x06;
const bit<16> TYPE_IPV4 = 0x800;
const bit<19> ECN_THRESHOLD = 10;

/*************************************************************************
*********************** H E A D E R S  ***********************************
*************************************************************************/

typedef bit<9>  egressSpec_t;
typedef bit<48> macAddr_t;
typedef bit<32> ip4Addr_t;

header ethernet_t {
    macAddr_t dstAddr;
    macAddr_t srcAddr;
    bit<16>   etherType;
}

/*
 * TODO: split tos to two fields 6 bit diffserv and 2 bit ecn
 */
header ipv4_t {
    bit<4>    version;
    bit<4>    ihl;
    //bit<8>    tos;
    bit<6>    diffserv;
    bit<2>    ecn;
    bit<16>   totalLen;
    bit<16>   identification;
    bit<3>    flags;
    bit<13>   fragOffset;
    bit<8>    ttl;
    bit<8>    protocol;
    bit<16>   hdrChecksum;
    ip4Addr_t srcAddr;
    ip4Addr_t dstAddr;
}

struct metadata {
}

struct headers {
    ethernet_t   ethernet;
    ipv4_t       ipv4;
}

/*************************************************************************
*********************** P A R S E R  ***********************************
*************************************************************************/

parser MyParser(packet_in packet,
                out headers hdr,
                inout metadata meta,
                inout standard_metadata_t standard_metadata) {

    state start {
        transition parse_ethernet;
    }

    state parse_ethernet {
        packet.extract(hdr.ethernet);
        transition select(hdr.ethernet.etherType) {
            TYPE_IPV4: parse_ipv4;
            default: accept;
        }
    }

    state parse_ipv4 {
        packet.extract(hdr.ipv4);
        transition accept;
    }
}


/*************************************************************************
************   C H E C K S U M    V E R I F I C A T I O N   *************
*************************************************************************/

control MyVerifyChecksum(inout headers hdr, inout metadata meta) {   
    apply {  }
}


/*************************************************************************
**************  I N G R E S S   P R O C E S S I N G   *******************
*************************************************************************/

control MyIngress(inout headers hdr,
                  inout metadata meta,
                  inout standard_metadata_t standard_metadata) {
    action drop() {
        mark_to_drop(standard_metadata);
    }
    
    action ipv4_forward(macAddr_t dstAddr, egressSpec_t port) {
        standard_metadata.egress_spec = port;
        hdr.ethernet.srcAddr = hdr.ethernet.dstAddr;
        hdr.ethernet.dstAddr = dstAddr;
        hdr.ipv4.ttl = hdr.ipv4.ttl - 1;
    }

    table ipv4_lpm {
        key = {
            hdr.ipv4.dstAddr: lpm;
        }
        actions = {
            ipv4_forward;
            drop;
        }
        size = 1024;
        default_action = drop;
    }
    
    apply {
        if (hdr.ipv4.isValid()) {
            ipv4_lpm.apply();
        }
    }
}

/*************************************************************************
****************  E G R E S S   P R O C E S S I N G   *******************
*************************************************************************/

control MyEgress(inout headers hdr,
                 inout metadata meta,
                 inout standard_metadata_t standard_metadata) {
    apply {
        /*
         * TODO:
         * - if ecn is 1 or 2
         *   - compare standard_metadata.enq_qdepth with threshold 
         *     and set hdr.ipv4.ecn to 3 if larger
         */
        //1和2表示主机支持ECN
        if(hdr.ipv4.ecn == 1 || hdr.ipv4.ecn == 2)
        {
            //enq_qdepth 为数据包排入队列时的队列深度，以数据包个数为单位
            if(standard_metadata.enq_qdepth > ECN_THRESHOLD)
            {
                //打上拥塞标记
                hdr.ipv4.ecn = 3;
            }
        }
    }
}

/*************************************************************************
*************   C H E C K S U M    C O M P U T A T I O N   **************
*************************************************************************/

control MyComputeChecksum(inout headers hdr, inout metadata meta) {
    apply {
        /* TODO: replace tos with diffserve and ecn */
	update_checksum(
	    hdr.ipv4.isValid(),
            { hdr.ipv4.version,
              hdr.ipv4.ihl,
              //hdr.ipv4.tos,
              hdr.ipv4.diffserv,
              hdr.ipv4.ecn,
              hdr.ipv4.totalLen,
              hdr.ipv4.identification,
              hdr.ipv4.flags,
              hdr.ipv4.fragOffset,
              hdr.ipv4.ttl,
              hdr.ipv4.protocol,
              hdr.ipv4.srcAddr,
              hdr.ipv4.dstAddr },
            hdr.ipv4.hdrChecksum,
            HashAlgorithm.csum16);
    }
}

/*************************************************************************
***********************  D E P A R S E R  *******************************
*************************************************************************/

control MyDeparser(packet_out packet, in headers hdr) {
    apply {
        packet.emit(hdr.ethernet);
        packet.emit(hdr.ipv4);
    }
}

/*************************************************************************
***********************  S W I T C H  *******************************
*************************************************************************/

V1Switch(
MyParser(),
MyVerifyChecksum(),
MyIngress(),
MyEgress(),
MyComputeChecksum(),
MyDeparser()
) main;
```

### 实验结果

h1 向 h2 不断发包，同时 h11 向 h22 通过 iperf 软件高速发送 UDP 包
![ecn prepare command](.\image\ecn prepare command.png)

将 h2 收到的包信息保存在`h2.log`中，然后利用`grep tos h2.log`提取其中的 tos 字段。可见，刚开始 tos 字段始终为 0x1 ，但是随着队列建立，包开始积累，于是中途 tos 字段变为了 0x3，最后，随着包队列逐渐清空，tos 字段又重新变回了 0x1。
![ecn changed tos](.\image\ecn changed tos.png)

## MRI 实验

### mri.p4

```c
/* -*- P4_16 -*- */
#include <core.p4>
#include <v1model.p4>

const bit<8>  UDP_PROTOCOL = 0x11;
const bit<16> TYPE_IPV4 = 0x800;
//在 ipv4 的可选字段里用31表示接下来是 MRI 头
const bit<5>  IPV4_OPTION_MRI = 31;

#define MAX_HOPS 9

/*************************************************************************
*********************** H E A D E R S  ***********************************
*************************************************************************/

typedef bit<9>  egressSpec_t;
typedef bit<48> macAddr_t;
typedef bit<32> ip4Addr_t;
typedef bit<32> switchID_t;
typedef bit<32> qdepth_t;

header ethernet_t {
    macAddr_t dstAddr;
    macAddr_t srcAddr;
    bit<16>   etherType;
}

header ipv4_t {
    bit<4>    version;
    bit<4>    ihl;
    bit<8>    diffserv;
    bit<16>   totalLen;
    bit<16>   identification;
    bit<3>    flags;
    bit<13>   fragOffset;
    bit<8>    ttl;
    bit<8>    protocol;
    bit<16>   hdrChecksum;
    ip4Addr_t srcAddr;
    ip4Addr_t dstAddr;
}

header ipv4_option_t {
    bit<1> copyFlag;
    bit<2> optClass;
    bit<5> option;	//全1表示后有MRI报头
    bit<8> optionLength;
}

header mri_t {
    bit<16>  count;
}

header switch_t {
    //交换机编号 32bit
    switchID_t  swid;
    //队列深度 32bit
    qdepth_t    qdepth;
}

struct ingress_metadata_t {
    bit<16>  count;
}

struct parser_metadata_t {
    //在解析时用于记录剩余MRI的switch头部个数
    bit<16>  remaining;
}

struct metadata {
    ingress_metadata_t   ingress_metadata;
    parser_metadata_t   parser_metadata;
}

struct headers {
    ethernet_t         ethernet;
    ipv4_t             ipv4;
    ipv4_option_t      ipv4_option;
    mri_t              mri;
    switch_t[MAX_HOPS] swtraces;
}

error { IPHeaderTooShort }

/*************************************************************************
*********************** P A R S E R  ***********************************
*************************************************************************/

parser MyParser(packet_in packet,
                out headers hdr,
                inout metadata meta,
                inout standard_metadata_t standard_metadata) {

    state start {
        transition parse_ethernet;
    }

    state parse_ethernet {
        packet.extract(hdr.ethernet);
        transition select(hdr.ethernet.etherType) {
            TYPE_IPV4: parse_ipv4;
            default: accept;
        }
    }

    state parse_ipv4 {
        packet.extract(hdr.ipv4);
        //ipv4报头长度必须大于等于5*4Byte
        verify(hdr.ipv4.ihl >= 5, error.IPHeaderTooShort);
        transition select(hdr.ipv4.ihl) {
            5             : accept;
            //大于5说明有MRI
            default       : parse_ipv4_option;
        }
    }

    state parse_ipv4_option {
        /*
        * TODO: Add logic to:
        * - Extract the ipv4_option header.
        *   - If value is equal to IPV4_OPTION_MRI, transition to parse_mri.
        *   - Otherwise, accept.
        */
        packet.extract(hdr.ipv4_option);
        transition select(hdr.ipv4_option.option)
        {
            //选项值为31时继续解析MRI头
            IPV4_OPTION_MRI: parse_mri;
            default:accept;
        }
    }

    state parse_mri {
        /*
        * TODO: Add logic to:
        * - Extract hdr.mri.
        * - Set meta.parser_metadata.remaining to hdr.mri.count
        * - Select on the value of meta.parser_metadata.remaining
        *   - If the value is equal to 0, accept.
        *   - Otherwise, transition to parse_swtrace.
        */
        packet.extract(hdr.mri);
        //记录下交换机跳数，以便循环解析swtraces
        meta.parser_metadata.remaining = hdr.mri.count;
        transition select(meta.parser_metadata.remaining)
        {
            0: accept;
            default: parse_swtrace;
        }
    }

    state parse_swtrace {
        /*
        * TODO: Add logic to:
        * - Extract hdr.swtraces.next.
        * - Decrement meta.parser_metadata.remaining by 1
        * - Select on the value of meta.parser_metadata.remaining
        *   - If the value is equal to 0, accept.
        *   - Otherwise, transition to parse_swtrace.
        */
        //解析一层含交换机数据的报头
        packet.extract(hdr.swtraces.next);
        //剩余数量减一
        meta.parser_metadata.remaining = meta.parser_metadata.remaining-1;
        transition select(meta.parser_metadata.remaining)
        {
            //一个不剩，停止解析
            0: accept;
            //继续解析
            default: parse_swtrace;
        }
    }
}


/*************************************************************************
************   C H E C K S U M    V E R I F I C A T I O N   *************
*************************************************************************/

control MyVerifyChecksum(inout headers hdr, inout metadata meta) {
    apply {  }
}


/*************************************************************************
**************  I N G R E S S   P R O C E S S I N G   *******************
*************************************************************************/

control MyIngress(inout headers hdr,
                  inout metadata meta,
                  inout standard_metadata_t standard_metadata) {
    action drop() {
        mark_to_drop(standard_metadata);
    }

    action ipv4_forward(macAddr_t dstAddr, egressSpec_t port) {
        standard_metadata.egress_spec = port;
        hdr.ethernet.srcAddr = hdr.ethernet.dstAddr;
        hdr.ethernet.dstAddr = dstAddr;
        hdr.ipv4.ttl = hdr.ipv4.ttl - 1;
    }

    table ipv4_lpm {
        key = {
            hdr.ipv4.dstAddr: lpm;
        }
        actions = {
            ipv4_forward;
            drop;
            NoAction;
        }
        size = 1024;
        default_action = NoAction();
    }

    apply {
        if (hdr.ipv4.isValid()) {
            ipv4_lpm.apply();
        }
    }
}

/*************************************************************************
****************  E G R E S S   P R O C E S S I N G   *******************
*************************************************************************/

control MyEgress(inout headers hdr,
                 inout metadata meta,
                 inout standard_metadata_t standard_metadata) {
    action add_swtrace(switchID_t swid) {
        /*
        * TODO: add logic to:
        - Increment hdr.mri.count by 1
        - Add a new swtrace header by calling push_front(1) on hdr.swtraces.
        - Set hdr.swtraces[0].swid to the id parameter
        - Set hdr.swtraces[0].qdepth to (qdepth_t)standard_metadata.deq_qdepth
        - Increment hdr.ipv4.ihl by 2
        - Increment hdr.ipv4.totalLen by 8
        - Increment hdr.ipv4_option.optionLength by 8
        */
        hdr.mri.count = hdr.mri.count+1;
        hdr.swtraces.push_front(1);
        //P4_16将新push的报头设为了inValid，所以需要手动将其设为valid
        hdr.swtraces[0].setValid();
        //swid 为本台交换机的编号
        hdr.swtraces[0].swid = swid;
        //从标准元数据中读取报文第一次进入时的队列深度，并进行强制类型转换
        hdr.swtraces[0].qdepth = (qdepth_t)standard_metadata.deq_qdepth;
        //ihl的单位是4Byte即32bit，故需+2
        hdr.ipv4.ihl = hdr.ipv4.ihl+2;
        hdr.ipv4.totalLen = hdr.ipv4.totalLen+8;
        hdr.ipv4_option.optionLength = hdr.ipv4_option.optionLength+8;
    }

    table swtrace {
        actions = {
            add_swtrace;
            NoAction;
        }

        default_action =  NoAction();
    }

    apply {
        /*
        * TODO: add logic to:
        * - If hdr.mri is valid:
        *   - Apply table swtrace
        */
        //仅对含有MRI头的报文添加交换机路径
        if (hdr.mri.isValid()) swtrace.apply();
    }
}

/*************************************************************************
*************   C H E C K S U M    C O M P U T A T I O N   **************
*************************************************************************/

control MyComputeChecksum(inout headers hdr, inout metadata meta) {
     //ipv4的首部校验和不包括可选字段，故此处无须修改
     apply {
        update_checksum(
            hdr.ipv4.isValid(),
            { hdr.ipv4.version,
              hdr.ipv4.ihl,
              hdr.ipv4.diffserv,
              hdr.ipv4.totalLen,
              hdr.ipv4.identification,
              hdr.ipv4.flags,
              hdr.ipv4.fragOffset,
              hdr.ipv4.ttl,
              hdr.ipv4.protocol,
              hdr.ipv4.srcAddr,
              hdr.ipv4.dstAddr },
            hdr.ipv4.hdrChecksum,
            HashAlgorithm.csum16);
    }
}

/*************************************************************************
***********************  D E P A R S E R  *******************************
*************************************************************************/

control MyDeparser(packet_out packet, in headers hdr) {
    apply {
        packet.emit(hdr.ethernet);
        packet.emit(hdr.ipv4);
        packet.emit(hdr.ipv4_option);
        packet.emit(hdr.mri);
        packet.emit(hdr.swtraces);

        /* TODO: emit ipv4_option, mri and swtraces headers */
    }
}

/*************************************************************************
***********************  S W I T C H  *******************************
*************************************************************************/

V1Switch(
MyParser(),
MyVerifyChecksum(),
MyIngress(),
MyEgress(),
MyComputeChecksum(),
MyDeparser()
) main;
```

### 实验结果

h1 向 h2 不断发包，同时 h11 向 h22 通过 iperf 软件高速发送 UDP 包，h2 通过 MRI 获取了包经过的交换机编号以及进入交换机时对应的队列长度
![mri switchTrace](.\image\mri switchTrace.png)

提取 swtraces 信息可以看到队列长度先变长后变短的过程
![mri swtraces](.\image\mri swtraces.png)

## 问答题

+ ecn和mri的实验对于数据包的头部字段操作和前面的实验有何不同？

  首先，这两次实验都是在基于原本 ipv4 的报头框架下对其功能进行拓展，ecn 利用了 tos 字段，mri 利用了可选字段，从而使其它交换机也能正常转发该报文。其次，mri 实验中的报头为变长报头，需要设计循环状态来对报头进行解析。

+ 查找资料，传统网络在拥塞控制和多跳路由检测方面是如何处理的？

  + 传统网络的拥塞控制由发送方维护一个拥塞窗口`cwnd`，只要网络没有出现拥塞，拥塞窗口就逐渐变大，但只要出现拥塞，拥塞窗口就迅速变小。具体有慢开始、拥塞避免、快重传、快恢复四种算法。而判断网络是否拥塞的依据是有没有按时收到确认报文，发送方无法准确评估网络拥塞程度。
  + 多跳路由检测主要有`BFD`和`NQA`两种方法来实现网络性能探测。如果需要实现 MRI 实验的路由路径跟踪，则可以使用命令`tracert`（Windows）或命令`traceroute`（Linux）。它们的原理是通过递增 ipv4 的 TTL 字段，并利用 ICMP 差错报文来不断探测下一条的路由地址，从而逐渐探测出整条路径。

## 提高题 ecn

### mycontroller.py
```python
#!/usr/bin/env python3
import argparse
import grpc
import os
import sys
from time import sleep

# Import P4Runtime lib from parent utils dir
# Probably there's a better way of doing this.
sys.path.append(
    os.path.join(os.path.dirname(os.path.abspath(__file__)),
                 '../utils/'))
import p4runtime_lib.bmv2
from p4runtime_lib.error_utils import printGrpcError
from p4runtime_lib.switch import ShutdownAllSwitchConnections
import p4runtime_lib.helper

#port topology
#s1
S1_TO_H1 = 2
S1_TO_H11 = 1
S1_TO_S2 = 3
S1_TO_S3 = 4
#s2
S2_TO_H2 = 2
S2_TO_H22 = 1
S2_TO_S1 = 3
S2_TO_S3 = 4
#s3
S3_TO_H3 = 1
S3_TO_S1 = 2
S3_TO_S2 = 3

def writeIpv4Rules(p4info_helper, sw, dst_eth_addr, dst_ip_addr, ip_mask, port_id):
    """
    write rules to table ipv4_lpm
    :param p4info_helper: the P4Info helper
    :param sw: the witch connection
    :param dst_eth_addr: the destination IP to match in the ingress rule
    :param dst_ip_addr: the destination Ethernet address to write in the
                        egress rule
    :param port_id: the egress port
    """
    table_entry = p4info_helper.buildTableEntry(
        table_name = "MyIngress.ipv4_lpm",
        match_fields = {
            "hdr.ipv4.dstAddr": (dst_ip_addr, ip_mask)
        },
        action_name = "MyIngress.ipv4_forward",
        action_params = {
            "dstAddr": dst_eth_addr,
            "port": port_id
        })
    sw.WriteTableEntry(table_entry)
    print("Installed rule on %s" % sw.name)


def readTableRules(p4info_helper, sw):
    """
    Reads the table entries from all tables on the switch.

    :param p4info_helper: the P4Info helper
    :param sw: the switch connection
    """
    print('\n----- Reading tables rules for %s -----' % sw.name)
    for response in sw.ReadTableEntries():
        for entity in response.entities:
            entry = entity.table_entry
            # TODO For extra credit, you can use the p4info_helper to translate
            #      the IDs in the entry to names
            #获取入口流表名并打印
            table_name = p4info_helper.get_tables_name(entry.table_id)
            print('table name: %s' % table_name)
            #获取匹配字段并打印
            print('match field:')
            for m in entry.match:
                print('    ', end='');
                print(p4info_helper.get_match_field_name(table_name, m.field_id), end=' ')
                print('%r' % (p4info_helper.get_match_field_value(m),))
            #打印流表中的行为名
            action = entry.action.action
            action_name = p4info_helper.get_actions_name(action.action_id)
            print('action name: %s' % action_name)
            #打印行为的传入参数
            print('action params:')
            for p in action.params:
                print('    ', end='');
                print(p4info_helper.get_action_param_name(action_name, p.param_id), end='')
                print(': %r' % p.value)
            print()


def printCounter(p4info_helper, sw, counter_name, index):
    """
    Reads the specified counter at the specified index from the switch. In our
    program, the index is the tunnel ID. If the index is 0, it will return all
    values from the counter.

    :param p4info_helper: the P4Info helper
    :param sw:  the switch connection
    :param counter_name: the name of the counter from the P4 program
    :param index: the counter index (in our case, the tunnel ID)
    """
    for response in sw.ReadCounters(p4info_helper.get_counters_id(counter_name), index):
        for entity in response.entities:
            counter = entity.counter_entry
            print("%s %s %d: %d packets (%d bytes)" % (
                sw.name, counter_name, index,
                counter.data.packet_count, counter.data.byte_count
            ))

def main(p4info_file_path, bmv2_file_path):
    # Instantiate a P4Runtime helper from the p4info file
    p4info_helper = p4runtime_lib.helper.P4InfoHelper(p4info_file_path)

    try:
        # Create a switch connection object for s1 and s2;
        # this is backed by a P4Runtime gRPC connection.
        # Also, dump(导出) all P4Runtime messages sent to switch to given txt files.
        s1 = p4runtime_lib.bmv2.Bmv2SwitchConnection(
            name='s1',
            address='127.0.0.1:50051',
            device_id=0,
            proto_dump_file='logs/s1-p4runtime-requests.txt')
        s2 = p4runtime_lib.bmv2.Bmv2SwitchConnection(
            name='s2',
            address='127.0.0.1:50052',
            device_id=1,
            proto_dump_file='logs/s2-p4runtime-requests.txt')
        s3 = p4runtime_lib.bmv2.Bmv2SwitchConnection(
            name='s3',
            address='127.0.0.1:50053',
            device_id=2,
            proto_dump_file='logs/s3-p4runtime-requests.txt')

        # Send master arbitration update message to establish this controller as
        # master (required by P4Runtime before performing any other write operation)
        s1.MasterArbitrationUpdate()
        s2.MasterArbitrationUpdate()
        s3.MasterArbitrationUpdate()

        # Install the P4 program on the switches
        s1.SetForwardingPipelineConfig(p4info=p4info_helper.p4info,
                                       bmv2_json_file_path=bmv2_file_path)
        print("Installed P4 Program using SetForwardingPipelineConfig on s1")
        s2.SetForwardingPipelineConfig(p4info=p4info_helper.p4info,
                                       bmv2_json_file_path=bmv2_file_path)
        print("Installed P4 Program using SetForwardingPipelineConfig on s2")
        s3.SetForwardingPipelineConfig(p4info=p4info_helper.p4info,
                                       bmv2_json_file_path=bmv2_file_path)
        print("Installed P4 Program using SetForwardingPipelineConfig on s3")

        #install the rules of table ipv4_lpm
        #s1
        print("install rules on s1...")
        writeIpv4Rules(p4info_helper, sw=s1, dst_eth_addr="08:00:00:00:01:01",
                         dst_ip_addr="10.0.1.1", ip_mask=32, port_id=S1_TO_H1)
        writeIpv4Rules(p4info_helper, sw=s1, dst_eth_addr="08:00:00:00:01:11",
                         dst_ip_addr="10.0.1.11", ip_mask=32, port_id=S1_TO_H11)
        writeIpv4Rules(p4info_helper, sw=s1, dst_eth_addr="08:00:00:00:02:00",
                         dst_ip_addr="10.0.2.0", ip_mask=24, port_id=S1_TO_S2)
        writeIpv4Rules(p4info_helper, sw=s1, dst_eth_addr="08:00:00:00:03:00",
                         dst_ip_addr="10.0.3.0", ip_mask=24, port_id=S1_TO_S3)
        #s2
        print("install rules on s2...")
        writeIpv4Rules(p4info_helper, sw=s2, dst_eth_addr="08:00:00:00:02:02",
                         dst_ip_addr="10.0.2.2", ip_mask=32, port_id=S2_TO_H2)
        writeIpv4Rules(p4info_helper, sw=s2, dst_eth_addr="08:00:00:00:02:22",
                         dst_ip_addr="10.0.2.22", ip_mask=32, port_id=S2_TO_H22)
        writeIpv4Rules(p4info_helper, sw=s2, dst_eth_addr="08:00:00:00:01:00",
                         dst_ip_addr="10.0.1.0", ip_mask=24, port_id=S2_TO_S1)
        writeIpv4Rules(p4info_helper, sw=s2, dst_eth_addr="08:00:00:00:03:00",
                         dst_ip_addr="10.0.3.0", ip_mask=24, port_id=S2_TO_S3)
        #s3
        print("install rules on s3...")
        writeIpv4Rules(p4info_helper, sw=s3, dst_eth_addr="08:00:00:00:03:03",
                         dst_ip_addr="10.0.3.3", ip_mask=32, port_id=S3_TO_H3)
        writeIpv4Rules(p4info_helper, sw=s3, dst_eth_addr="08:00:00:00:01:00",
                         dst_ip_addr="10.0.1.0", ip_mask=24, port_id=S3_TO_S1)
        writeIpv4Rules(p4info_helper, sw=s3, dst_eth_addr="08:00:00:00:02:00",
                         dst_ip_addr="10.0.2.0", ip_mask=24, port_id=S3_TO_S2)

        readTableRules(p4info_helper, s1)
        readTableRules(p4info_helper, s2)
        readTableRules(p4info_helper, s3)

    except grpc.RpcError as e:
        printGrpcError(e)

    ShutdownAllSwitchConnections()

if __name__ == '__main__':
    #创建解析对象
    parser = argparse.ArgumentParser(description='P4Runtime Controller')
    #向解析对象中添加关注的命令行参数或选项
    parser.add_argument('--p4info', help='p4info proto in text format from p4c',
                        type=str, action="store", required=False,
                        default='./build/ecn.p4.p4info.txt')
    parser.add_argument('--bmv2-json', help='BMv2 JSON file from p4c',
                        type=str, action="store", required=False,
                        default='./build/ecn.json')
    #解析命令行参数
    args = parser.parse_args()

    if not os.path.exists(args.p4info):
        parser.print_help()
        print("\np4info file not found: %s\nHave you run 'make'?" % args.p4info)
        parser.exit(1)
    if not os.path.exists(args.bmv2_json):
        parser.print_help()
        print("\nBMv2 JSON file not found: %s\nHave you run 'make'?" % args.bmv2_json)
        parser.exit(1)
    main(args.p4info, args.bmv2_json)

```

### 实验结果

首先修改`topology.json`文件，将`switches`中各个交换机的`runtime`删除
![delete original runtime](.\image\delete original runtime.png)

之后执行`make`初始化，然后在另一终端上运行`.\mycontroller.py`
![install rules](.\image\install rules.png)

最后在 mininet 上执行`pingall`查看结果
![pingall](.\image\pingall.png)

可见所有主机已成功连通

## 实验总结

+ 通过课件和 P4 spec 文档学习了 header stack 的用法。
+ 在知了社区和 CSDN 上学习了传统拥塞控制和多跳路由检测的方法。
+ 进一步加深了对 P4 工作逻辑和 runtime 控制平面编写的理解。
+ 利用 P4 标准元数据就可以读取每个交换机的队列信息，进一步利用 P4 修改报头就可以非常便捷而又高效地实现拥塞控制和多跳路由检测，与传统方式相比有了质的突破。

## GitHub 仓库链接
[Luoky 的仓库](https://github.com/Luoky76/P4-exercise) `https://github.com/Luoky76/P4-exercise`